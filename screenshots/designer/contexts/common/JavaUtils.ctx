--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: JavaUtils
Element-Type: context
Element-Version: 2.0
Id: _oP0WUAmoEe2Qztd66l64Xw
Runtime-Version: 2.5.3.202205021448
Save-Time: 7/22/22, 12:25 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc openType[val typeInput][val select]
{
  get-eclipse-window | key-type "M1+M2+t"
  with [get-window "Open Type"] {
    get-editbox -after [get-label "Enter type.*"] | set-text $typeInput
    loop [val count 0]{
      try{
        get-window "Open Type" | get-table | select [concat $select ".*"] | click -default
      } -catch {
        log [format "failed to select %s" $select]
        if [ $count | lt 10]{
          wait -ms 500
          recur [$count | plus 1]
        }
        throw-error [concat "Cannot find type <" $typeInput "> with selection <" $select ">"]
      }
    }
  }
}

proc createSourceDir[val project][val dirName]{
  get-view "Axon Ivy Projects" | get-tree | select $project | get-menu -path "New/Other..." | click -metaKeys ""
  with [get-window "Select a wizard"] {
    get-editbox -after [get-label "Wizards:"] | set-text source
    get-button "Next >" | click -metaKeys ""
  }
  with [get-window "New Source Folder"] {
    get-editbox -after [get-label "Folder name:"] | set-text $dirName
    get-button Finish | click -metaKeys ""
  }
}

proc addJunit5Library[val editor]{
  with [get-editor $editor | get-text-viewer] {
    set-caret-pos 12 6
    key-type "M1+1"
  }
  get-window -from "CompletionProposalPopup.createProposalSelector()" | get-table
    | select "Add JUnit 5 library to the build path" | click -default
}

proc assertJunitOk[val testUnit][val -name waitSeconds -value 20]{
  with [get-view JUnit | get-tree] {
    waitItemExists -seconds $waitSeconds -itemName [format "%s \\[Runner: JUnit 5\\].*" $testUnit ]
        | get-property "image.path"
        | equals "org.eclipse.jdt.junit/icons/full/obj16/tsuite.png" | verify-true
  }
}

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
