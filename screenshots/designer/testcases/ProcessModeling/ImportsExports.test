--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _y5zpEF73Eea4DszzhZF9qA
Element-Name: ImportsExports
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _Tiq00F78Eea4DszzhZF9qA
Runtime-Version: 2.5.3.202205021448
Save-Time: 6/1/22, 3:54 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

importProject -projectName "documentationExampleProject"

OpenView CMS
get-view CMS | close
with [get-view "Axon Ivy Projects"] {
	get-button "Collapse All.*" | click
	get-tree | get-item "documentationExampleProject/pom.xml" | click
	get-tree | get-item "documentationExampleProject" | click
}

get-eclipse-window | get-object | invoke setSize 1300 800
find-tab-folder "Axon Ivy Projects" | get-object | captureScreenshot "ivy-project/project-tree-full.png"

get-view "Axon Ivy Projects" | get-tree | get-menu "New/Axon Ivy Project" | click
with [get-window "New Axon Ivy Project"] {
	get-editbox -after [get-label "Project Name:"] | set-text InvoiceManager
	get-editbox -after [get-label "Group ID:"] | set-text "com.acme.ivy"
	get-editbox -after [get-label "Project ID:"] | set-text invoice
	wait 500
	saveWindowContentScreenshot "ivy-project/new-project-wizard-1.png"
	get-button "Next >" | click
	get-table | get-item "documentationExampleProject .*" | check
	saveWindowContentScreenshot "ivy-project/new-project-wizard-2.png"
	get-button "Next >" | click
	get-editbox -after [get-label "Name:"] | set-text "Invoice"
	saveWindowContentScreenshot "ivy-project/new-project-wizard-3.png"
	get-button "Next >" | click
	saveWindowContentScreenshot "ivy-project/new-project-wizard-4.png"
	get-button Finish | click
}

try{
	get-eclipse-window | get-object | invoke setSize 950 650
	get-view "Axon Ivy Projects" | get-tree | select "InvoiceManager/pom.xml" | double-click

	find-tab-folder InvoiceManager | get-object | captureScreenshot "ivy-project/deployment-descriptor-editor-deployment.png"

	get-eclipse-window | get-object | invoke setSize 1000 700
	get-editor InvoiceManager | get-button "Show project graph" | get-object | invoke getParent | invoke getParent | invoke
		"getParent" | captureScreenshot "ivy-project/project-graph-view-open-action.png"

	with [get-editor InvoiceManager | get-tab-folder] {
		get-tab-item Deployment | click
		get-tab-item "Required Projects" | click
	}
	get-editor InvoiceManager | get-tab-folder | get-tab-item "Required Projects" | click
	find-tab-folder InvoiceManager | get-object | captureScreenshot "ivy-project/deployment-descriptor-editor-required-projects.png"
	get-view "Project Graph" | get-button "Manually refreshes workspace library graph  and re-arranges the layout." | click

	get-eclipse-window | resizeWindow 1300 800
	find-tab-folder "Project Graph" | get-object | captureScreenshot "ivy-project/project-graph-view.png"


	get-view "Axon Ivy Projects" | get-tree | get-menu "Import..." | click
	get-window Import | get-tree | select "General/Existing Projects into Workspace" | double-click
	set-dialog-result Folder [format "%s/additionalProjects/%s" [substitute-variables "${system_property:screenshots}"] "QuickStartTutorial" | str]
	with [get-window Import] {
		resizeWindow 520 560
		get-window Import | get-button "Select root directory:" | click
		get-button "Browse..." | click
		get-group Options | get-button "Copy projects into workspace" | uncheck
		wait 500
		saveWindowContentScreenshot "ivy-project/project-import.png"
		get-button "Select All" | click
		get-button Cancel | click
	}

	get-view "Axon Ivy Projects" | get-tree | get-menu "Import..." | click
	get-window Import | get-tree | select "Axon Ivy/Ivy Archive \\(\\*.iar\\)" | double-click
	set-dialog-result Folder [format "%s/additionalProjects" [substitute-variables "${system_property:screenshots}"] ]
	with [get-window "Import ivy Archives.*"] {
		get-button "Browse ..." | click
		get-button "Select All" | click
		get-button "Copy projects into workspace" | check
		resizeWindow 500 390
		saveWindowContentScreenshot "ivy-project/project-import-wizard.png"
		get-button Cancel | click
	}
}-finally{
	deleteProject "InvoiceManager"
}

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
