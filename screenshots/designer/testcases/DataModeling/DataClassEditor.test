--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: DataClassEditor
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _nL-4cBVAEeeAwNHEHXL8Yw
Runtime-Version: 2.5.5.202408111356
Save-Time: 10/21/24, 2:12â€¯PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc setTableColumnWidth [val name] [val size]
{
	get-column-header $name | get-object | invoke setWidth $size
}

proc openDataClass [val path]
{
	get-view "Axon Ivy Projects" | get-tree | get-item $path | double-click
}

// DataClass editor general
importProject -projectName "QuickStartTutorial"
togglePresentation -flat true

proc dataClassEditor{ 
	openDataClass -path "QuickStartTutorial/Data Classes/quickStartTutorial/Data"
	get-eclipse-window | resizeWindow 700 500
	with [get-editor Data]{
		maximize
	}
	find-tab-folder Data | get-object | captureScreenshot "data-class/data-class-editor.png"
	get-editor Data | close
} 

proc newDataClassWizard{
	get-view "Axon Ivy Projects" | get-tree | select "QuickStartTutorial" | get-menu 
		-path "New/Data Class" | click -metaKeys ""
	with [get-window "New Data Class"] {
		resizeWindow 500 350
		get-editbox -after [get-label "Data Class Name:"] | set-text Person
		get-object | captureScreenshot "data-class/data-class-new-wizard.png"
		get-button Cancel | click -metaKeys ""
	}
}

proc newEntityClassWizard{
	get-view "Axon Ivy Projects" | get-tree | select "QuickStartTutorial" | get-menu 
		-path "New/Entity Class" | click -metaKeys ""
	with [get-window "New Entity Class"] {
		resizeWindow 500 450
		get-editbox -after [get-label "Entity Class Name:"] | set-text Customer
		get-editbox -after [get-label "Entity DB Table Name:"] | set-text ERP_CUSTOMER
		get-object | captureScreenshot "persistence/entity-class-new-wizard.png"
		get-button Cancel | click -metaKeys ""
	}
}

proc caseMapSample{
	importProject -projectName "documentationExampleProject"
	openDataClass -path "documentationExampleProject/Data Classes/com.axonivy/CreditDossier"
	get-eclipse-window | resizeWindow 500 600
	find-tab-folder CreditDossier | get-object | captureScreenshot "casemap/credit-dossier-data.png"
}

dataClassEditor
newDataClassWizard
newEntityClassWizard
caseMapSample

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
