--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Process
Element-Type: context
Element-Version: 2.0
Id: _PRrTwNcsEeaidO8WBzHpig
Runtime-Version: 2.5.0.202001211718
Save-Time: 2/27/20, 11:15 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc openInscription[val processName] [val elementName] [val elementType]
{
	with [get-view "Axon.ivy Projects" | get-tree] {
		let [val processPath [format "documentationExampleProject/Processes/%s" $processName ]]
		{
    		get-item $processPath | double-click
		}
	}

	OpenView -viewName Outline
	let [val outlineElementName [format "%s/%s : %s" $processName $elementName $elementType]]
	{
		get-view Outline | get-tree | select $outlineElementName | double-click
		waitWindowExists [format "Inscribe %s" $elementType]
	}
}

proc openHtmlDialogInscription[val dialogName] [val elementName] [val elementType]
{
	with [get-view "Axon.ivy Projects" | get-tree] {
		let [val processPath [format "documentationExampleProject/Html Dialogs/screenshot.project.%s/%s Logic" $dialogName $dialogName]]
		{
    		get-item $processPath | double-click
		}
	}

	OpenView -viewName Outline
	let [val outlineElementName [format "%sProcess/%s : %s" $dialogName $elementName $elementType]]
	{
		get-view Outline | get-tree | select $outlineElementName | double-click
		waitWindowExists [format "Inscribe %s" $elementType]
	}
}

proc captureInscription [val elementType][val tabName][val screenshot][val width][val height]
{
	let [val inscriptionEditor [format "Inscribe %s" $elementType]]
	{
		let [val targetPath [format "process-elements/%s" $screenshot]]
		{
			with [get-window $inscriptionEditor]
			{
				resizeWindow -width $width -height $height
			    with [get-tab-folder]
			    {
			        get-tab-item $tabName | click
				    wait -ms 2000 // wait for UI animation (radio switches, button highlight, etc.)
					get-object | captureScreenshot $targetPath
			    }
				get-button Cancel | click
			}
		}
	}
}

proc captureInscriptionPart [val screenshot]
{
	let [val targetPath [format "process-elements/%s" $screenshot]]
	{
	    wait -ms 1000 // wait for UI animation (radio switches, button highlight, etc.)
		get-object | captureScreenshot $targetPath
	}
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
