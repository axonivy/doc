--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: WindowUtils
Element-Type: context
Element-Version: 2.0
Id: _MHuYEFIjEemi0ZP8qibuzA
Runtime-Version: 2.3.0.201806262310
Save-Time: 3/29/19 2:12 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc waitWindowExists [val windowName]
{
	waitWindowExistsMaxRetries $windowName 10
}

proc waitWindowExistsMaxRetries [val windowName] [val retries]
{
	loop [val retryCount 0]
	{
		try
		{
			log [format "waiting for window %s" $windowName]
			get-window $windowName
		} -catch
		{
			if [lt $retryCount $retries]
			{
				wait -ms 500
				recur [$retryCount | plus 1]
			}
		}
	}
}

proc waitViewExists [val viewName]
{
	loop
	{
		try
		{
			log [format "waiting for view %s" $viewName]
			get-view $viewName
		} -catch -
		{
			wait -ms 200
			recur
		}
	}
}

// This procedure needs to be called within a 'with []' block!
proc waitItemExists [val itemName]
{
	try -times 10 -delay 1000 -command {
		get-item $itemName
	} -catch {
		throw-error [concat "Could not load item with name " $itemName]
	}
}

proc waitWindowInvisible [val windowName]
{
	loop
	{
		try
		{
			log [format "waiting until window %s is gone" $windowName]
			with [get-window $windowName]
			{
				get-button OK | click
			}
			wait -ms 500
			recur
		} -catch
		{
		}
	}
}


proc resizeWindow [val "window" -input] [val width] [val height]
{
	$window | maximize
	$window | get-object | invoke setSize $width $height
	wait 500
}

proc openPerspective [val perspectiveName]
{
	get-menu "Window/Perspective/Open Perspective/Other..." | click
	with [get-window "Open Perspective"] {
	    get-table | select $perspectiveName
	    get-button Open | click
	}
}

proc "openView" [val viewName]
{
	get-menu -path "Window/Show View/Other..." | click
  	with [get-window "Show View"] {
		get-editbox | set-text $viewName
		get-tree | select [format ".*/%s" $viewName]
		get-button Open | click
  	}
}

proc openOutlineView
{
	openView "Outline"
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
