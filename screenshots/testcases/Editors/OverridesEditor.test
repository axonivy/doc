--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _y5zpEF73Eea4DszzhZF9qA,_uUaD0AbNEeqelOY6v4JKGw
Element-Name: OverridesEditor
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _LIWKQO5ZEemOcbNM5ZUdlw
Runtime-Version: 2.5.0.201909012000
Save-Time: 10/22/19, 9:56 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc overrideSubProcess [val project] [val subProcessToOverride] [val overridingSubProcess -value ""]
{
	openDefinition $project "Overrides"

	get-editor $project -type "Override Editor" | get-section "Sub Process Overrides" | get-button "Create a new override.*" | click
	with [get-window "New Component Override"]
	{
	    get-group "Select component to override (original)" | get-button "Select component to override" | click
	    get-window "Select Process" | get-table | select [concat $subProcessToOverride ".*"] | double-click
	    if [ $overridingSubProcess | not-eq "" ]
	    {
	    	get-group "Override definition.*" | get-button "Select overriding component" | click
	    	get-window "Select Process" | get-table | select [concat $overridingSubProcess ".*"] | double-click
	    }
	    set-dialog-result MessageBox 32 //SWT.OK
	    get-button Finish | click
	}
}

proc overrideHtmlDialog [val project] [val dialogToOverride] [val overridingDialog -value ""]
{
	openDefinition $project "Overrides"

	get-editor $project -type "Override Editor" | get-section "Html Dialog Overrides" | get-button "Create a new override.*" | click
	with [get-window "New Component Override"]
	{
	    get-group "Select component to override (original)" | get-button "Select component to override" | click
	    get-window "Select Dialog" | get-table | select [concat $dialogToOverride ".*"] | double-click
	    if [ $overridingDialog | not-eq "" ]
	    {
	    	get-group "Override definition.*" | get-button "Select overriding component" | click
	    	get-window "Select Dialog" | get-table | select [concat $overridingDialog ".*"] | double-click
	    }
	    set-dialog-result MessageBox 32 //SWT.OK
	    get-button Finish | click
	}
}

proc deleteNotWantedCmsOverrides [val project]
{
	with [get-editor $project | get-section "Content Object Overrides"] {
	    with [get-table] {
	        select "\\/Images\\/Icon" | key-type "M1+a"
	    }
	    get-button "Delete the selected overrides." | click
	}
	get-window "Delete selected item(s)?" | get-button Delete | click
}

proc takeWizardScreenshot [val project] [val dialogName]
{
	closeAllEditors
	openDefinition $project "Overrides"
	with [get-editor $project]
	{
		get-section "Html Dialog Overrides" | get-button "Create a new override.*" | click
		with [get-window "New Component Override"]
		{
		    get-group "Override definition (replacement)" | get-button "Select overriding component" | click
		    with [get-window "Select Dialog"] {
		        get-table | select [concat $dialogName ".*"]
	            get-button OK | click
	        }
	        saveWindowContentScreenshot "designer-configuration/overrides-wizard.png"
	        get-button Cancel | click
		}

	}
}

proc closeAllEditors
{
	get-menu -path "File/Close All" | click
}

proc createOverrides [val webShop] [val acmeWebShop]
{
	createSubProcess $webShop "calculatePrice"
	createSubProcess $acmeWebShop "calculateResellerPrice"
	overrideSubProcess $acmeWebShop "calculatePrice" "calculateResellerPrice"

	createHtmlDialog $webShop "ch.ivy.webshop" "shoppingBasket"
	createHtmlDialog $acmeWebShop "ch.ivy.webshop.acme" "shoppingBasket"
	takeWizardScreenshot $acmeWebShop "shoppingBasket"

	overrideHtmlDialog $acmeWebShop "shoppingBasket" "shoppingBasket"
	closeAllEditors

	openDefinition $acmeWebShop "Overrides"
	deleteNotWantedCmsOverrides $acmeWebShop
}

let [val webShop "Web_shop"] [val acmeWebShop "Acme_web_shop"]{
	try{
		createProject $webShop
		createProject $acmeWebShop
		addRequiredProject $acmeWebShop $webShop
		togglePresentation -flat true
		createOverrides $webShop $acmeWebShop

		with [get-view "Axon.ivy Projects" | get-tree]{
			get-item [concat $acmeWebShop "/Definitions/Overrides"] | double-click
		}

		get-eclipse-window | resizeWindow 1050 850
		with [get-editor $acmeWebShop]{
			maximize
			get-control Any -after [get-label "Override Definitions"] | get-object | captureScreenshot "designer-configuration/overrides-editor.png"
		}

		get-editor $acmeWebShop | close
	} -finally {
		deleteProject $acmeWebShop
		deleteProject $webShop
	}
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
