--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: ProjectUtils
Element-Type: context
Element-Version: 2.0
Id: _lbFrAAbTEeqelOY6v4JKGw
Save-Time: 10/13/21, 12:54 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc "import-ivy-projects" [val archiveGroup] {
	get-menu "File/Import..." | click
	get-window Import | get-tree | select "Axon Ivy/Ivy Projects.*" | double-click
	with [get-window "Ivy Projects Import"] {
		get-tree| get-item $archiveGroup | check
    	get-button Finish | click
	}
	wait 1000
}

proc packProject [val projectName]
{
	get-view "Axon Ivy Projects" | get-tree | select $projectName | get-menu -path "Pack Project (*.iar)" | click
}

proc unpackProject [val projectName]
{
	get-view "Axon Ivy Projects" | get-tree | select $projectName | get-menu -path "Unpack Project (*.iar)" | click
}

proc deleteProject [val projectName]
{
	get-view "Axon Ivy Projects" | get-tree | select $projectName | get-menu -path Delete | click
	with [get-window Refactoring] {
	    get-button "Delete project contents on disk (cannot be undone)" | check
	    get-button Finish | click
	}
}

proc deleteAllProjects
{
	get-view "Axon Ivy Projects" | get-tree | select ".*" -all | get-menu -path Delete | click
	with [get-window Refactoring] {
	    get-button "Delete project contents on disk (cannot be undone)" | check
	    get-button Finish | click
	}
}

proc openConfig [val projectName] [val config]
{
	with [get-view "Axon Ivy Projects" | get-tree]
	{
		get-item [concat $projectName "/Config/" $config] | double-click
	}
}

proc openPom [val projectName]
{
	with [get-view "Axon Ivy Projects" | get-tree]
	{
		get-item [concat $projectName "/pom.xml"] | double-click
	}
}

proc addRequiredProject [val project] [val requiredProject]
{
	openPom $project
	with [get-editor $project]
	{
		get-tab-folder | get-tab-item "Required Projects" | click
		get-section "Required Projects" | get-button Add | click
	}

	with [get-window "Add Required Project"]
	{
	    get-combo -after [get-label "Select project:"] | select [concat $requiredProject ".*"]
	    get-button OK | click
	}
	get-button "Save .*" | click
}

proc assertNoRunningOperations {
	measure-time "CheckRunningOperations" {
		loop {
			try {
				get-view Progress | get-button "Cancel Operation"
				recur
			} -catch {
				// Exit loop when no cancel operation available
			}
		}
	} | constrain-max duration 10000
}

proc togglePresentation[val -name "navigator" -value "Axon Ivy Projects"][val flat]
{
	if [$flat]{
		get-view $navigator | get-menu -path "Package Presentation/Flat" | click
	} -else {
		get-view $navigator| get-menu -path "Package Presentation/Hierarchical" | click
	}
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
